pkgname=dxvk-mingw-git
pkgver=2.6.1.r29.gfe9a24d15
pkgrel=1
arch=(x86_64)
makedepends=(git glslang meson mingw-w64-gcc ninja)
depends=(bash vulkan-icd-loader wine)
source=(dxvk::git+https://github.com/doitsujin/dxvk.git setup_dxvk.sh setup_dxvk)
options=(!lto)

pkgver() {
    cd dxvk
    printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g')"
}

prepare() {
    cd dxvk
    git remote set-url origin https://github.com/doitsujin/dxvk.git
    git submodule update --init --filter=tree:0 --recursive
}

build() {
    export LDFLAGS="${LDFLAGS/-fuse-ld=lld/}"
    meson setup dxvk "build/x64" \
        --cross-file dxvk/build-win64.txt \
        --prefix /usr/share/dxvk/x64 \
        --bindir "" --libdir "" \
        --buildtype release \
        --strip
    ninja -C "build/x64" -v

    meson setup dxvk "build/x32" \
        --cross-file dxvk/build-win32.txt \
        --prefix /usr/share/dxvk/x32 \
        --bindir "" --libdir "" \
        --buildtype release \
        --strip
    ninja -C "build/x32" -v
}

package() {
    DESTDIR="$pkgdir" ninja -C "build/x32" install
    DESTDIR="$pkgdir" ninja -C "build/x64" install
    install -Dm 755 -t "$pkgdir/usr/share/dxvk" setup_dxvk.sh
    install -Dm 644 -t "$pkgdir/usr/share/dxvk" dxvk/dxvk.conf
    install -Dm 755 -t "$pkgdir/usr/bin" setup_dxvk
}
