pkgname=glslang-git
pkgver=15.3.0.r3.g963588074
pkgrel=1
arch=(x86_64)
makedepends=(cmake ninja spirv-headers python)
depends=(gcc-libs spirv-tools)
groups=(vulkan-devel)
provides=(glslang)
conflicts=(glslang)
source=(glslang::git+https://github.com/KhronosGroup/glslang.git)

pkgver() {
  cd glslang
  git describe --long --tags --exclude main-tot --exclude sdk\* --exclude SDK-candidate\* --exclude untagged\* | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cmake -S glslang -B _build -G Ninja \
        -D BUILD_SHARED_LIBS=ON \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D ALLOW_EXTERNAL_SPIRV_TOOLS=ON \
        -Wno-dev
    cmake --build _build
}

package() {
    DESTDIR="${pkgdir}" cmake --install _build
}
