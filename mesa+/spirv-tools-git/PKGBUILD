pkgname=spirv-tools-git
pkgver=2025.2.rc2.r37.g66fe61094
pkgrel=1
arch=(x86_64)
makedepends=(cmake git ninja python spirv-headers)
depends=(gcc-libs glibc sh)
groups=(vulkan-devel)
provides=(spirv-tools)
conflicts=(spirv-tools)
source=(SPIRV-Tools::git+https://github.com/KhronosGroup/SPIRV-Tools.git)

pkgver() {
    cd SPIRV-Tools
	git describe --long --tags | sed 's/^[^0-9]*//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
  cmake -S SPIRV-Tools -B _build -G Ninja \
        -D BUILD_SHARED_LIBS=ON \
        -D SPIRV_TOOLS_BUILD_STATIC=OFF \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D SPIRV-Headers_SOURCE_DIR=/usr \
        -Wno-dev
  cmake --build _build
}

package() {
  DESTDIR="$pkgdir" cmake --install _build
}
